viewpoint:
    param int nVars;
    var bool x[nVars];
    param int q[nVars];
    param int k;

predicates:
amk(var bool x[], param int q[], param int k) {
    var bool s[sizeof(x)+1][k];

	//c init
	forall(j in 0..k-1) {
		!s[0][j];
	};

    //c clausules 1r constraint
	forall(i in 1..sizeof(s)-1, j in 0..k-1) {
		s[i-1][j] -> s[i][j];
	};

    //c clausules 2n constraint
	forall(i in 1..sizeof(s)-1, j in 0..q[i-1]-1) {
		x[i-1] -> s[i][j];
	};

    //c clausules 3r constraint
    forall(i in 1..sizeof(s)-1, j in 0..k-1-q[i-1]) { // Totes les q son > 0 i <= k
        (s[i-1][j] & x[i-1]) -> s[i][j+q[i-1]];
	};

    //c clausules 4t constraint
	forall(i in 1..sizeof(x)) {
		s[i-1][k-q[i-1]] -> !x[i-1];
	};
}

constraints:
    // Instancia competicio maxsat unweighted
    amk(x, q, k);
    forall(i in 0..sizeof(x)-1) {
        x[i] @1;
    };

output:
    "PB formula = " ++ [ q[i] ++ "*" ++ (x[i] ? "1" : "0") ++ (i < nVars-1 ? " + " : "") | i in 0..nVars-1];
    "Result = " ++ sum([ q[i] * (x[i] ? 1 : 0) | i in 0..nVars-1]);